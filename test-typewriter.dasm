;;
;; ~/0x10c/dcpu-el/test-typewriter.dasm ---
;;
;; $Id: test-typewriter.dasm,v 1.5 2012/04/21 05:27:39 harley Exp $
;;
;; Might need to eval these lines with "C-x C-e":
;;   (load "~/0x10c/dcpu-el/dasm-mode")
;;   (dasm-mode)

:start
        set     pc,loop_start

:loop_start
        set     i,0

;; LKEY_xxxx labels jump here.
:loop_char
        ; wrap to start
        and     i,0x007f
        ; draw the cursor
        set     [0x8000+i],0x23 ; '#'
        set     a,0
        ; This is GETC, but "a16" doesnt have asm for it.
        word    0x0020 ; getc a
        set     [0x8000+i],0x20 ; ' '
        ; no char read
        ife     a,0
          set     pc,loop_char
        ; delete?
        ife     a,0x7F
          set     pc,LKEY_delete
        ; normal?
        ifg     a,0x1f
          set     pc,LKEY_selfinsert
        ; try ctl keys
        set     pc,LKEY_a
        word    0xFFFF

:LKEY_a
        ifn     a,0x01 ; ^A
          set     pc,LKEY_k
        jsr     do_startofline
        set     pc,loop_char

:LKEY_k
        ifn     a,0x0B ; ^K
          set     pc,LKEY_m
        jsr     do_killline
        set     pc,loop_char
        word    0xffff

:LKEY_m
        ifn     a,0x0D ; ^M
          set     pc,LKEY_z
        jsr     do_nextline
        jsr     do_startofline
        jsr     do_killline
        set     pc,loop_char
        word    0xffff

:LKEY_z
        ifn     a,0x1a ; ^z
          set     pc,LKEY_selfinsert
        set     i,0
        jsr     do_killline
        set     pc,loop_char
        word    0xFFFF

:LKEY_delete
        ifg     i,0
          sub     i,1
        set     [0x8000+i],0
        set     pc,loop_char
        word    0xFFFF

:LKEY_selfinsert ; self insert char
        set     [0x8000+i],a
        add     i,1
        set     pc,loop_char
        word    0xffff

:do_nextline
        jsr     do_startofline
        add     i,0x20
        and     i,0x007f
        set     pc,pop
        word    0xffff

:do_startofline
        and     i,0xffe0
        set     pc,pop
        word    0xFFFF

:do_killline
        set     a,i
:do_killline_1
        set     [0x8000+a],0
        add     a,1
        set     c,a
        and     c,0x1F
        ife     c,0
          set     pc,pop
        set     pc,do_killline_1
        word    0xffff
